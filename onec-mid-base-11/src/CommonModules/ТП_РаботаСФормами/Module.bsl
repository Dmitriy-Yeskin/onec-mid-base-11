
#Область ПрограммныйИнтерфейс

Процедура ДоработатьФорму(Форма) Экспорт
	
	ВывестиРеквизитКонтактноеЛицо(Форма);
	
	//ДобавитьСкидкуВЗаказПокупателя(Форма);
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


//Добавляет реквизит "КонтактноеЛицо" в формы всех докукментов
Процедура ВывестиРеквизитКонтактноеЛицо(Форма) 
	
	Если Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Или
		Форма.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Или
		Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПоле(Форма, "КонтактноеЛицо", "Объект.ТП_КонтактноеЛицо", Форма.Элементы.ГруппаШапкаПраво,);
	ИначеЕсли Форма.ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПоле(Форма, "КонтактноеЛицо", "Объект.ТП_КонтактноеЛицо", , Форма.Элементы.Основание);
	ИначеЕсли Форма.ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПоле(Форма, "КонтактноеЛицо", "Объект.ТП_КонтактноеЛицо", , Форма.Элементы.СуммаДокумента);
	КонецЕсли;
	
КонецПроцедуры


// Добавляет на форму документа ЗаказПокупетеля интерфейс для функционала установки скидки
Процедура ДобавитьСкидкуВЗаказПокупателя(Форма) 
	
	Если Форма.ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьГруппуГруппаСкидки(Форма);
		ДобавитьПоле(Форма, "СогласованнаяСкидка", "Объект.ТП_СогласованнаяСкидка", Форма.Элементы.ГруппаСкидка,);
		
		ДобавляемоеПоле = Форма.Элементы.Найти("СогласованнаяСкидка");
		ДобавляемоеПоле.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
		
		ДобавитьКомандуПересчитатьТаблицу(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПоле(Форма, Реквизит, ПутьКДанным, ЭлементРодитель, ПередЭлементом)
	
	Если Форма.Элементы.Найти(Реквизит) = Неопределено Тогда
		ДобавляемоеПоле = Форма.Элементы.Вставить(Реквизит, Тип("ПолеФормы"), ЭлементРодитель, ПередЭлементом);
		ДобавляемоеПоле.Вид = ВидПоляФормы.ПолеВвода;
		ДобавляемоеПоле.ПутьКДанным = ПутьКДанным;
	КонецЕсли;
	
КонецПроцедуры


Процедура ДобавитьГруппуГруппаСкидки(Форма)
	
	Если Форма.Элементы.Найти("ГруппаСкидка") = Неопределено Тогда
		ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
		ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		ГруппаСкидка.ОтображатьЗаголовок = Ложь
	КонецЕсли;

КонецПроцедуры


Процедура ДобавитьКомандуПересчитатьТаблицу(Форма)
	
	Если Форма.Команды.Найти("ПересчитатьТаблицу") = Неопределено Тогда
		КомандаПТ = Форма.Команды.Добавить("ПересчитатьТаблицу");
		КомандаПТ.Заголовок = "Пересчитать таблицу";
		КомандаПТ.Действие = "КомандаПересчитатьТаблицу";
	КонецЕсли;
	
	Если Форма.Элементы.Найти("КнопкаПересчитатьТаблицу") = Неопределено Тогда
		
		КнопкаФормыПТ = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
		КнопкаФормыПТ.ИмяКоманды = "ПересчитатьТаблицу";
		КнопкаФормыПТ.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		КнопкаФормыПТ.Картинка = БиблиотекаКартинок.Обновить;
		КнопкаФормыПТ.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 